       SUBROUTINE W3TAGB(PROG,KYR,JD,LF,ORG)
C$$$   SUBR PROGRAM DOCUMENTATION BLOCK
C
C SUBROUTINE: W3TAGB         OPERATIONAL JOB IDENTIFIER
C   AUTHOR: JACK NEWELL           W/NMC42    DATE: 85-04-08
C
C ABSTRACT: PRINTS IDENTIFYING INFORMATION FOR OPERATIONAL
C   JOBS. CALLED AT THE BEGINNING OF A JOB, W3TAGB PRINTS
C   THE JOB NAME, THE YEAR AND JULIAN DAY OF ITS
C   COMPILATION, AND THE RESPONSIBLE ORGANIZATION. CALLED
C   AT THE END OF A JOB, W3TAGE PRINTS A LINE STATING THE
C   JOB NAME AND THAT IT HAS ENDED.
C
C PROGRAM HISTORY LOG:
C   85-04-08  JACK NEWELL
C   89-06-30  R.E.JONES   CONVERT TO MICROSOFT FORTRAN 4.10
C   90-06-04  R.E.JONES   CONVERT TO SUN FORTRAN 1.3
C   91-03-29  R.E.JONES   CONVERT TO SiliconGraphics FORTRAN
C   91-06-01  R.E.JONES   ADD DATE-TIME
C   93-03-29  R.E.JONES   ADD SAVE STATEMENT
C   93-11-10  R.E.JONES   ADD DAY OF YEAR, ADD DAY OF WEEK,
C                         ADD JULIAN DAY NUMBER
C
C USAGE:  CALL W3TAGB(PROG,KYR,JD,LF,ORG)
C         CALL W3TAGE(PROG)
C
C   INPUT VARIABLES:
C     NAMES  INTERFACE DESCRIPTION OF VARIABLES AND TYPES
C     ------ --------- -----------------------------------------------
C     PROG   ARG LIST  PROGRAM NAME   CHARACTER*8
C     KYR    ARG LIST  YEAR OF COMPILATION   INTEGER*4
C     JD     ARG LIST  JULIAN DAY OF COMPILATION   INTEGER*4
C     LF     ARG LIST  HUNDRETHS OF JULIAN DAY OF COMPILATION
C                      INTEGER *4  (RANGE IS 0 TO 99 INCLUSIVE)
C     ORG    ARG LIST  ORGANIZATION CODE (SUCH AS WD42)
C                      CHARACTER*6
C
C   OUTPUT VARIABLES:
C     NAMES  INTERFACE DESCRIPTION OF VARIABLES AND TYPES
C     ----------------------------------------------------------------
C     DDATE  PRINT     YEAR AND JULIAN DAY (NEAREST HUNDRETH)
C            FILE      OF COMPILATION  REAL*8
C
C   SUBPROGRAMS CALLED:
C               W3LIB:  DATIMX
C
C   REMARKS: DOUBLE PRECISION USED IN ORDER TO HAVE AT LEAST
C            SEVEN DECIMAL DIGITS ACCURACY FOR VALUE OF DDATE
C
C   WARNING: IDATE, TIME ARE NOT STANDARD FORTRAN 77, THIS 
C            SUBROUTINE IS NOT PORTABLE WITH OUT CHANGES.
C
C ATTRIBUTES:
C   LANGUAGE: DEC FORTRAN V3.60 FOR OSF/1 AXP SYSTEMS
C   MACHINE:  DEC ALPHA 400/710  190MHz
C
C$$$
C
         REAL*8      DDATE
         REAL*8      DLF
         REAL*8      DJD
         REAL*8      DYR
C
         INTEGER     NOW(14)
C
         CHARACTER*6 ORG
         CHARACTER*8 PROG
         CHARACTER*3 JMON(12)
         CHARACTER*3 DAYW(7)
C
         SAVE
C
         DATA  JMON/'JAN','FEB','MAR','APR','MAY','JUN',
     &              'JUL','AUG','SEP','OCT','NOV','DEC'/
         DATA  DAYW/'SUN','MON','TUE','WEN','THU','FRI','SAT'/
C
         DYR   = KYR
         DYR   = 1.0D+03 * DYR
         DJD   = JD
         DLF   = LF
         DLF   = 1.0D-02 * DLF
         DDATE = DYR + DJD + DLF
         PRINT 600
  600    FORMAT(//,5X,18('* . '))
         PRINT 601, PROG, DDATE, ORG
  601    FORMAT(10X,'PROGRAM ',A8,' HAS BEGUN. COMPILED ',F8.2,
     &   5X, 'ORGANIZATION ',A6)
         CALL DATIMX(NOW)
         PRINT 602, JMON(NOW(7)),NOW(6),NOW(8),NOW(5),NOW(4),
     &   NOW(3),NOW(2),NOW(13),DAYW(NOW(12)),NOW(9)
  602    FORMAT(10X,'STARTING DATE-TIME  ',A3,1X,I2.2,',',
     &   I4.4,2X,2(I2.2,':'),I2.2,'.',I3.3,2X,I3,2X,A3,2X,I8,//)
         RETURN
C
         ENTRY W3TAGE(PROG)
C
         CALL DATIMX(NOW)
         PRINT 603, JMON(NOW(7)),NOW(6),NOW(8),NOW(5),NOW(4),
     &   NOW(3),NOW(2),NOW(13),DAYW(NOW(12)),NOW(9)
  603    FORMAT(//,10X,'ENDING DATE-TIME    ',A3,1X,I2.2,',',
     &   I4.4,2X,2(I2.2,':'),I2.2,'.',I3.3,2X,I3,2X,A3,2X,I8)
         PRINT 604,PROG
  604    FORMAT(10X,'PROGRAM ',A8,' HAS ENDED.  DEC ALPHA',
     &   ' model 4000/710  190MHz')
         PRINT 605
  605    FORMAT(5X,18('* . '))
         RETURN
         END
